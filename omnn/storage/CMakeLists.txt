option(OPENMIND_STORAGE_FOUNDATIONDB "Use Apple FoundationDB library for one of the multistorage levels" NO)
option(OPENMIND_STORAGE_LEVELDB "Use Google LevelDB library for one of the multistorage levels" ON)
option(OPENMIND_STORAGE_REDIS "Use Redis for caching" ON)

set(DEPENDENCIES rt)
if(OPENMIND_STORAGE_LEVELDB OR OPENMIND_STORAGE_FOUNDATIONDB OR OPENMIND_STORAGE_REDIS)
    option(OPENMIND_STORAGE_ALLOW_UPGRADE "Update optimized cached values during fetch if new optimizations applied" ON)
    option(OPENMIND_STORAGE_VOLATILE "Clean cache each start (for debugging purposes)" NO)

    if(OPENMIND_STORAGE_LEVELDB)
        find_package(Threads) # workaround leveldb config bug
        list(APPEND DEPENDENCIES google/leveldb)
        if(NOT WIN32)
            list(APPEND DEPENDENCIES pthread)
        endif()
    endif()

    if(OPENMIND_STORAGE_FOUNDATIONDB)
        list(APPEND DEPENDENCIES ohhmm/FoundationDB)
    endif()

    if(OPENMIND_STORAGE_REDIS)
        list(APPEND DEPENDENCIES Boost::redis)
    endif()

endif(OPENMIND_STORAGE_LEVELDB OR OPENMIND_STORAGE_FOUNDATIONDB OR OPENMIND_STORAGE_REDIS)

if(OPENMIND_STORAGE_REDIS)
    find_package(hiredis 0.13.0 REQUIRED)
    if(NOT TARGET hiredis::hiredis)
        message(FATAL_ERROR "hiredis target not found. Please ensure hiredis is properly installed with vcpkg or system package manager.")
    endif()
    list(APPEND DEPENDENCIES hiredis::hiredis)
    add_compile_definitions(HIREDIS_VERSION="${hiredis_VERSION}")
    message(STATUS "Redis support enabled. Using hiredis version ${hiredis_VERSION}")
    if(WIN32)
        message(STATUS "On Windows, ensure Redis server is running at localhost:6379 for tests")
    endif()
endif()

lib(${DEPENDENCIES})

if(OPENMIND_STORAGE_LEVELDB)
    target_compile_definitions(${this_target} PUBLIC OPENMIND_STORAGE_LEVELDB)
endif()
if(OPENMIND_STORAGE_FOUNDATIONDB)
    target_compile_definitions(${this_target} PUBLIC OPENMIND_STORAGE_FOUNDATIONDB)
endif()
if(OPENMIND_STORAGE_REDIS)
    target_compile_definitions(${this_target} PUBLIC OPENMIND_STORAGE_REDIS)
endif()
if(OPENMIND_STORAGE_ALLOW_UPGRADE)
    target_compile_definitions(${this_target} PUBLIC ALLOW_UPGRADE OPENMIND_STORAGE_ALLOW_UPGRADE)
endif()
